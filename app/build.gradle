apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

apply plugin: 'jacoco'


jacoco {
  toolVersion = "0.7.1.201405082137"
}
def coverageSourceDirs = [
  'src/main/groovy',
]

task jacocoTestReport(type: JacocoReport, dependsOn: ["compileDebugSources"]) {
  group = "Reporting"
  description = "Generate Jacoco coverage reports after running tests."
  reports {
    xml.enabled = true
    html.enabled = true
  }
  classDirectories = fileTree(
      dir: './build/intermediates/classes/debug',
      excludes: ['com/myapp/R*.class',
      '**/*$InjectAdapter.class',
      '**/*$ModuleAdapter.class',
      '**/*$ViewInjector*.class'
  ])
  sourceDirectories = files(coverageSourceDirs)
  executionData = files('build/jacoco/testDebug.exec')
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "app.georchestra.beneth.fr.georchestra"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    packagingOptions {
      exclude 'META-INF/DISCLAIMER'
      exclude 'META-INF/LICENSE'
      exclude 'META-INF/NOTICE'
      exclude 'META-INF/NOTICE.txt'
    }
    sourceSets {
      main {
        java {
          srcDirs = ['src/main/groovy']
        }
      }
      test {
        java {
          srcDirs = ['src/test/groovy']
        }
      }
    }

    testOptions {
      resultsDir = "${project.buildDir}/tests/results"
    }
}
repositories {
    maven {
        url 'http://download.qualitystreetmap.org/maven/'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'org.codehaus.groovy:groovy:2.4.5:grooid'
    compile('org.codehaus.groovy:groovy-xml:2.4.5') {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    compile('fr.beneth:wxslib:1.1-SNAPSHOT') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
        exclude group: 'commons-collections', module: 'commons-collections'
    }

    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'org.osmdroid:osmdroid-android:4.3'
    compile 'org.slf4j:slf4j-simple:1.6.1'
}


